Dataset ecommerce_single_model {
  label: 'Ecommerce Single Model'
  __engine__: 'aql'
  description: 'Dataset used to write examples for AQL docs. DO NOT CHANGE OR DELETE.'
  data_source_name: 'demodb'
  models: [
    orders,
    order_items_ext,
    users
  ]
  relationships: [
    relationship(order_items_ext.user_id > users.id, true),
    relationship(order_items_ext.order_id > orders.id, true)
  ]
  owner: 'ha.pham@holistics.io'

  metric customer_aov {
    label: 'Customer AOV'
    type: 'number'
    definition: @aql 
      (order_items_ext.total_amount * 1.0 / count_distinct(order_items_ext.order_id)) | keep_grains(users) ;;
  }

  metric percent_of_total {
    label: 'Percent of Total'
    type: 'number'
    definition: @aql order_items_ext.total_amount * 1.0 / (order_items_ext.total_amount | exclude(order_items_ext)) ;;
  }

  metric running_total_orders {
    label: 'Running Total Orders over Month'
    type: 'number'
    definition: @aql
      orders.total_orders | running_total(run: orders.created_at | month())
     ;;
  }

  metric running_total_orders_reset_year {
    label: 'Running Total Orders over Months - Reset by Year'
    type: 'number'
    definition: @aql
      orders.total_orders | running_total(run: orders.created_at | month(), reset: orders.created_at | year())
     ;;
  }

  
}